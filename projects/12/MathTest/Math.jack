// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Math.jack

/** A library of commonly used mathematical functions.
 *  All functions runs in O(n), where n is the number of bits used
 *  for representing a two's complement integer value (16 in the Hack computer). 
 *  Note: Jack compilers implement multiplication and division
 *  using calls to OS functions in this class.
 */
class Math {
    static int n;             // Number of bits used for representing a two's complement integer
    static Array powersOfTwo; // Stores 2^0, 2^1, 2^2,..., 2^(n-1)

    // Initializes the Math library.
    function void init() {
        let n = 16;
        let powersOfTwo = Array.new(n);
        let powersOfTwo[0] = 1;
        let powersOfTwo[1] = 2;
        let powersOfTwo[2] = 4;
        let powersOfTwo[3] = 8;
        let powersOfTwo[4] = 16;
        let powersOfTwo[5] = 32;
        let powersOfTwo[6] = 64;
        let powersOfTwo[7] = 128;
        let powersOfTwo[8] = 256;
        let powersOfTwo[9] = 512;
        let powersOfTwo[10] = 1024;
        let powersOfTwo[11] = 2048;
        let powersOfTwo[12] = 4096;
        let powersOfTwo[13] = 8192;
        let powersOfTwo[14] = 16384;
        let powersOfTwo[15] = 16384+16384;  // Must be an off by 1 error in the supplied compiler :(
        return;
    }

    /** Returns the product of x and y. 
     *  When a Jack compiler detects the multiplication operator '*'
     *  in an expression, it handles it by invoking this method. 
     *  Thus, in Jack, x * y and Math.multiply(x,y) return the same value.
     */
    function int multiply(int x, int y) {
        var int sum, shiftedX, i;
        let sum = 0;
        let shiftedX = x;
        let i = 0;
        while(i < n) {
            if (~((powersOfTwo[i] & y) = 0)) {
                let sum = sum + shiftedX;
            }
            let shiftedX = shiftedX + shiftedX;
            let i = i + 1;
        }
        return sum;
    }

    /** Returns the integer part of x / y.
     *  When a Jack compiler detects the division operator '/'
     *  an an expression, it handles it by invoking this method.
     *  Thus, x/y and Math.divide(x,y) return the same value.
     */
    function int divide(int x, int y) {
        var int q, result;
        var bool neg;
        if ((x & powersOfTwo[15]) | (y & powersOfTwo[15])) {
            if ((x & powersOfTwo[15]) & (y & powersOfTwo[15])) { 
                let neg = false;
            } else {
                let neg = true;
            }
        }
        let x = Math.abs(x);
        let y = Math.abs(y);
        if (y > x) {
            return 0;
        }
        let q = x / (y+y);
        if (x - (2 * q * y) < y) {
            let result = 2 * q;
        } else {
            let result = (2 * q) + 1;
        }
        if (neg) {
            return -result;
        }
        return result;
    }

    /** Returns the integer part of the square root of x.
    */
    function int sqrt(int x) {
        var int low, high, mid, squared, ans;

        if ((x = 0) | (x = 1)) {
        return x;
        }

        let low = 1;
        let high = 181; // this squared leads to max int for hack platform, 2^16
        let ans = 0;

        while ((low < high) | (low = high)) {
            let mid = (high + low) / 2;
            let squared = mid * mid;

            if (squared = x) {
                return mid;
            }

            if (squared > x) {
                let high = mid - 1;
            } else {
                let low = mid + 1;
                let ans = mid;
            }
        }

        return ans;
    }

    /** Returns the greater value.
    */
    function int max(int a, int b) {
        if (a > b) {
            return a;
        }
        return b;
    }

    /** Returns the smaller value.
    */
    function int min(int a, int b) {
        if (a < b) {
            return a;
        }
        return b;
    }

    /** Returns the absolute value of x
    */
    function int abs(int x) {
        if (x < 0) {
            return (-1) * x;
        }
        return x;
    }
}
